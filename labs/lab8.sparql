#Jos√© Astorga
#AUX

#P0
LOAD <http://www.wikidata.org/entity/Q578595>
LOAD <http://www.wikidata.org/entity/Q131074>
LOAD <http://www.wikidata.org/entity/Q27894574>
LOAD <http://www.wikidata.org/entity/Q483815>
LOAD <http://www.wikidata.org/entity/Q5597803>
LOAD <http://www.wikidata.org/entity/Q170250> 
INTO GRAPH <https://www.wikidata.org>


#P00
CLEAR GRAPH <http://users.dcc.uchile.cl/~jastorga/foaf.ttl> 
LOAD <http://users.dcc.uchile.cl/~jastorga/foaf.ttl>
INTO GRAPH <http://users.dcc.uchile.cl/~jastorga/foaf.ttl>


######################################   P1   ######################################


SELECT COUNT(*) as ?triples
FROM <http://users.dcc.uchile.cl/~jastorga/foaf.ttl> #Default Graph
WHERE { ?p ?q ?s }


######################################   P2   ######################################


SELECT COUNT(DISTINCT ?g) as ?apariciones
WHERE { GRAPH ?g { ?p ?q <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja>} }

######################################   P3   ######################################


PREFIX foaf: <http://xmlns.com/foaf/0.1/>
SELECT DISTINCT ?name ?age 
FROM <http://users.dcc.uchile.cl/~jastorga/foaf.ttl>
WHERE { 
       <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> foaf:knows ?p .
       GRAPH ?g {
          ?p foaf:age ?age .
          ?p foaf:name ?name .
   }
} 


######################################   P4   ######################################

# Alternativa: Usando solo default graph
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
SELECT DISTINCT ?name
WHERE{
      <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> foaf:knows* ?p .
      ?p a foaf:Person; foaf:name ?name .
}



##############################################
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
SELECT DISTINCT ?name
FROM <http://users.dcc.uchile.cl/~jastorga/foaf.ttl>
WHERE{
      { <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> foaf:knows* ?q .
        GRAPH ?g {?q foaf:name ?name . }    
    }

    UNION
    {
      <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> foaf:knows ?p .
      GRAPH ?g { ?p a foaf:Person; foaf:knows* ?q .}
      GRAPH ?r { ?q a foaf:Person; foaf:name ?name . }  
    }
}


######################################   P5   ######################################

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ex: <http://example.org/>
INSERT {
        GRAPH <http://users.dcc.uchile.cl/~jastorga/foaf.ttl>{
            <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> ex:transitiveKnows ?p .
        }
    }
WHERE{
      <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> foaf:knows* ?p .
      ?p a foaf:Person .
}


######################################   P6   ######################################

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ex: <http://example.org/>
INSERT {
        GRAPH <http://users.dcc.uchile.cl/~jastorga/foaf.ttl>{
            <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> ex:stranger ?p .
        }
    }
WHERE{
        GRAPH ?g { ?p a foaf:Person . }
        FILTER NOT EXISTS{ 
            GRAPH <http://users.dcc.uchile.cl/~jastorga/foaf.ttl>{ 
                <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> ex:transitiveKnows ?p . }
        }
}


######################################   P7   ######################################

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ex: <http://example.org/>
INSERT {
    GRAPH <http://users.dcc.uchile.cl/~jastorga/foaf.ttl>{
        ?p foaf:name ?name .
    }
}
WHERE{
    GRAPH <http://users.dcc.uchile.cl/~jastorga/foaf.ttl>{ 
        <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> ex:stranger ?p . }
    GRAPH ?g { ?p foaf:name ?name . }
}

#####################################################################################

# TEST; Consulta (antes de insertar los datos: 0 resultados )
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ex: <http://example.org/>

SELECT DISTINCT ?p ?name
FROM <http://users.dcc.uchile.cl/~jastorga/foaf.ttl>
WHERE { <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> ex:stranger ?p .
        ?p foaf:name ?name}



######################################   P8   ######################################


PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ex: <http://example.org/>
SELECT DISTINCT ?name 
WHERE {
        {  
            ?q a foaf:Person . # todas las personas
        }
            MINUS{
            {
                <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> ex:transitiveKnows ?p . # conozco a un sujeto ?p (transitivamente)  
                ?q ex:transitiveKnows ?p .                                                  # sujeto ?q tambien conoce a ?p (transitivamente)
            }                                                                               # se puede reemplazar ex:transitiveKnows por foaf:knows* pero tarda mucho tiempo
            UNION
            {
                <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> ex:transitiveKnows ?q . # Tambien incluimos a las personas que conocemos transitivamente ya que se puede considerar que las conocemos recursivamente (hay un camino de nosotros a ellos)
            }
            UNION
            {
                ?q ex:transitiveKnows <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> . # Y los que me conoce a mi
            }
        }
        ?q foaf:name ?name
}
LIMIT 1

######################################   P9   ######################################

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ex: <http://example.org/>
SELECT DISTINCT ?person_name ?movie_name
FROM <http://users.dcc.uchile.cl/~jastorga/foaf.ttl>
WHERE {
        <https://users.dcc.uchile.cl/~jastorga/foaf.ttl#ja> foaf:interest ?movie . 
        GRAPH ?g { ?person foaf:interest ?movie .
                   ?person foaf:name ?person_name . }
        GRAPH ?r { ?movie rdfs:label ?movie_name}

        FILTER(?g != <http://users.dcc.uchile.cl/~jastorga/foaf.ttl>)
        FILTER (lang(?movie_name) = 'en')
}


######################################   P10   ######################################

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ex: <http://example.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?movie_name COUNT( DISTINCT ?person) as ?popularidad 
WHERE {
        GRAPH ?g { ?person foaf:interest ?movie . }
        GRAPH ?r { ?movie rdfs:label ?movie_name}
     FILTER (lang(?movie_name) = 'en')
        }
GROUP BY ?movie_name
ORDER BY DESC(?popularidad)



####################################################################################
